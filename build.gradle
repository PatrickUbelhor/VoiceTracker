buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE'
		classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'

group 'team.gif'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'

	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

task copyReact(type: Copy) {
	from 'react/build'
	into 'build/resources/main/static/.'
}

task appNpmInstall(type: NpmTask) {
	description = 'Installs all dependencies from package.json'
	workingDir = file("${project.projectDir}/react")
	args = ['install']
}

task appNpmBuild(type: NpmTask) {
	description = 'Builds production version of the webapp'
	workingDir = file("${project.projectDir}/react")
	args = ['run', 'build']
}

appNpmBuild.dependsOn appNpmInstall
copyReact.dependsOn appNpmBuild
compileJava.dependsOn copyReact

node {
	version = '10.6.0'
	npmVersion = '6.10.2'
	download = false
}
