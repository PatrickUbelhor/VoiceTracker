buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.3.2.RELEASE'
		classpath 'com.github.node-gradle:gradle-node-plugin:2.2.4'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.node-gradle.node'

group 'team.gif'
version '1.11.1'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'
}

task copyReact(type: Copy) {
	from 'react/build'
	into 'build/resources/main/static/.'
}

task appNpmBuild(type: NpmTask) {
	description = 'Builds production version of the webapp'
	args = ['run', 'build']
}

task appNpmInstall(type: NpmTask) {
	description = 'Installs all dependencies from package.json'
	args = ['install']
}

appNpmBuild.dependsOn appNpmInstall
copyReact.dependsOn appNpmBuild
compileJava.dependsOn copyReact

node {
	version = '14.5.0'
	npmVersion = '6.14.7'
	download = false
	nodeModulesDir = file("${project.projectDir}/react")
}
